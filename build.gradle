task bundleZip(type: Zip) {
    subprojects.stream().map(e -> e.tasks.build).forEach(e -> dependsOn(e))
    archiveFileName = 'modules.zip'
    destinationDirectory = file("$buildDir")
}

if (!project.tasks.findByName('assemble')) {
    task assemble {
    }
}

if (!project.tasks.findByName('build')) {
    task build {
    }
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'

    shadowJar {
        duplicatesStrategy = 'EXCLUDE'
        classifier = null
        configurations = [project.configurations.runtimeClasspath]
        exclude 'module-info.class'
    }

    parent.tasks.bundleZip.dependsOn tasks.shadowJar
    parent.tasks.bundleZip.from tasks.shadowJar
}

configure(subprojects.findAll { it.name.endsWith('_module') }) {
    dependencies {
        compileOnly project(':LaunchServer')
    }
}

configure(subprojects.findAll { it.name.endsWith('_swmodule') }) {
    dependencies {
        compileOnly project(':ServerWrapper')
    }
    shadowJar {
        relocate 'io.netty', 'pro.gravit.repackage.io.netty'
    }
}

configure(subprojects.findAll { it.name.endsWith('_lmodule') }) {
    dependencies {
        compileOnly project(':Launcher')
    }
    shadowJar {
        relocate 'org.objectweb.asm', 'pro.gravit.repackage.org.objectweb.asm'
        relocate 'io.netty', 'pro.gravit.repackage.io.netty'
    }
}

project.tasks.findByName('assemble').dependsOn tasks.bundleZip
project.tasks.findByName('build').dependsOn project.tasks.findByName('assemble')